/* Utility library */
#include <Utils.inc>

/* Error module */
#include "Error.inc"

/*****************************************************************************/
/*                               Error                                       */
/*****************************************************************************/

/* Did any error happen yet? */
def ErrorFlag: Int32T;

/*****************************************************************************/
/*                             ErrorInit                                     */
/*****************************************************************************/

def ErrorInit: func()
begin

   /* initialize error flag */
   ErrorFlag := FALSE;

end;

/*****************************************************************************/
/*                            ErrorTrigger                                   */
/*****************************************************************************/

def ErrorTrigger: func()
begin

   /* print empty line if this is the first error */
   if ErrorFlag == FALSE then
      UtilsConsoleWriteStr("\n");
   endif;

   /* set error flag */
   ErrorFlag := TRUE;

end;

/*****************************************************************************/
/*                            ErrorHeader                                    */
/*****************************************************************************/

def ErrorHeader: func(ErrorData: @ErrorDataT)
begin

   /* print error header */
   UtilsConsoleWriteStr("Front-end Error: ");

   /* if ErrorData is available, print more information */
   if ErrorData != NULL then

      /* print file name */
      UtilsConsoleWriteStr("In ");
      UtilsConsoleWriteStr(ErrorData@.FileName);
      UtilsConsoleWriteStr(":");

      /* print line number */
      UtilsConsoleWriteInt(ErrorData@.LineNo);
      UtilsConsoleWriteStr("\n");

   endif;

end;

/*****************************************************************************/
/*                            ErrorBodyStr                                   */
/*****************************************************************************/

def ErrorBodyStr: func(String: @StringT)
begin

   /* print error message */
   UtilsConsoleWriteStr(String);

end;

/*****************************************************************************/
/*                            ErrorBodyInt                                   */
/*****************************************************************************/

def ErrorBodyInt: func(Integer: Int32T)
begin

   /* print error message */
   UtilsConsoleWriteInt(Integer);

end;

/*****************************************************************************/
/*                             ErrorFooter                                   */
/*****************************************************************************/

def ErrorFooter: func(ErrorData: @ErrorDataT)

   /* loop counter */
   def I: Int32T;

begin

   /* if ErrorData is available, print more information */
   if ErrorData != NULL then

      /* print line */
      UtilsConsoleWriteStr(ErrorData@.Line);

      /* line doesn't contain \n? */
      if ErrorData@.Line@[ErrorData@.LineSize-1] != '\n' then
         UtilsConsoleWriteStr("\n");
      endif;

      /* print spaces */
      for I from 0 to ErrorData@.LineOff do
         UtilsConsoleWriteStr(" ");
      loop;

      /* now print the fancy ^ */
      UtilsConsoleWriteStr("^");

   endif;

   /* print error footer */
   UtilsConsoleWriteStr("\n\n");

end;
